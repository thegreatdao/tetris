<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	width="{initWidth}" 
	height="{initHeight}"
	xmlns:base="com.emptyyourmind.component.base.*"
	implements="com.emptyyourmind.component.ITetris">
	<mx:Script>
		<![CDATA[			
			import com.emptyyourmind.component.TetrisConst;			
			import com.emptyyourmind.common.TetrisUtil;
			
			private var phase:int = 0;
			private var _initWidth:int = 75;
			private var _initHeight:int = 50;
			
			[Bindable]
			public function set initWidth(width:int):void
			{
				_initWidth = width; 
			}
						
			public function get initWidth():int
			{
				return _initWidth;
			}
			
			[Bindable]
			public function set initHeight(height:int):void
			{
				_initHeight = height;	
			}
			
			public function get initHeight():int
			{
				return _initHeight;	
			}
			
			public function moveLeft():void
			{
				TetrisUtil.moveLeft(this);
			}
			
			public function moveRight():void
			{
				TetrisUtil.moveRight(this);	
			}
			
			public function moveDown():void
			{
				TetrisUtil.moveDown(this);	
			}
			
			public function mirrorMyself():void{}
			
			public function rotateMyself():void
			{
				removeAllChildren();
				TetrisUtil.swapDimension(this);
				
				if(phase == 0)
				{
					TetrisUtil.verticalFill(this);
					x = x + TetrisConst.UNIT_OF_MOVEMENT;
					removeChildAt(1);
					removeChildAt(4);
				}
				else if(phase == 1)
				{
					TetrisUtil.horizontalFill(this);
					x = x - TetrisConst.UNIT_OF_MOVEMENT;
					y = y + TetrisConst.UNIT_OF_MOVEMENT;
					removeChildAt(3);
					removeChildAt(4);
				}
				else if(phase == 2)
				{
					TetrisUtil.verticalFill(this);
					y = y - TetrisConst.UNIT_OF_MOVEMENT;
					removeChildAt(0);
					removeChildAt(3);
				}
				else
				{
					TetrisUtil.horizontalFill(this);
					removeChildAt(0);
					removeChildAt(1);
				}
				phase = (phase + 1) % 4;
			}
		]]>
	</mx:Script>
	<base:TetrisUnit x="0" y="25"/>
	<base:TetrisUnit x="25" y="25"/>
	<base:TetrisUnit x="25" y="0"/>
	<base:TetrisUnit x="50" y="25"/>
</mx:Canvas>
